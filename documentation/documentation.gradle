buildscript {
	dependencies {
		// upgrade to latest jruby version due to a bugfix needed for Windows 10.
		// can be removed, when asciidoctorj uses this as a default version.
		classpath('org.jruby:jruby-complete:9.1.17.0')

		// classpath('org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.16')
		classpath('org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16')
		classpath('org.asciidoctor:asciidoctorj-diagram:1.5.9')
	}
}

plugins {
	id 'org.asciidoctor.convert' version '1.5.8.1'
}

task consoleLauncherTest(type: JavaExec) {
	dependsOn testClasses
	def reportsDir = file("$buildDir/test-results")
	outputs.dir reportsDir
	classpath sourceSets.test.runtimeClasspath
	main 'org.junit.platform.console.ConsoleLauncher'
	args '--scan-classpath'
	args '--details', 'tree'
	args '--include-classname', '.*Tests'
	args '--include-classname', '.*Demo'
	args '--exclude-tag', 'exclude'
	args '--reports-dir', reportsDir
	systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
}
test {
	dependsOn consoleLauncherTest
	exclude '**/*'
}

dependencies {
	asciidoctor('org.jruby:jruby-complete:9.1.17.0')

	// Jupiter API is used in src/main/java
	implementation(project(':junit-jupiter-api'))

	testImplementation(project(':junit-jupiter-params'))
	testImplementation(project(':junit-platform-runner'))
	testImplementation(project(':junit-platform-launcher'))
	testImplementation("org.jetbrains.kotlin:kotlin-stdlib")

	// Required by :consoleLauncherTest and :generateConsoleLauncherOptions
	testRuntimeOnly(project(':junit-platform-console'))
	testRuntimeOnly(project(':junit-platform-suite-api'))
	testRuntimeOnly(project(':junit-platform-surefire-provider'))

	testRuntimeOnly(project(':junit-vintage-engine'))
	testRuntimeOnly(project(':junit-jupiter-engine'))
	testRuntimeOnly("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	testRuntimeOnly("org.apache.logging.log4j:log4j-jul:${log4jVersion}")

	// for ApiReportGenerator
	testImplementation('io.github.classgraph:classgraph:4.0.2')
}

asciidoctorj {
	version = '1.5.6'
}

ext {
	generatedAsciiDocPath = buildDir.toPath().resolve('generated/asciidoc')
	consoleLauncherOptionsFile = generatedAsciiDocPath.resolve('console-launcher-options.txt')
	experimentalApisTableFile = generatedAsciiDocPath.resolve('experimental-apis-table.txt')
	deprecatedApisTableFile = generatedAsciiDocPath.resolve('deprecated-apis-table.txt')
}

def createJavaExecTaskWithOutputFile(taskName, outputFile, mainClass, mainArgs = []) {
	task(taskName, type: JavaExec) {
		outputs.file outputFile.toFile()
		classpath = sourceSets.test.runtimeClasspath
		main = mainClass
		args = mainArgs
		standardOutput = new ByteArrayOutputStream()
		doLast {
			java.nio.file.Files.createDirectories(outputFile.parent)
			java.nio.file.Files.write(outputFile, standardOutput.toByteArray())
		}
	}
}

createJavaExecTaskWithOutputFile('generateConsoleLauncherOptions', consoleLauncherOptionsFile, 'org.junit.platform.console.ConsoleLauncher', ['--help'])

createJavaExecTaskWithOutputFile('generateExperimentalApisTable', experimentalApisTableFile, 'org.junit.api.tools.ApiReportGenerator', ['EXPERIMENTAL'])
createJavaExecTaskWithOutputFile('generateDeprecatedApisTable', deprecatedApisTableFile, 'org.junit.api.tools.ApiReportGenerator', ['DEPRECATED'])

asciidoctor {
	dependsOn 'generateConsoleLauncherOptions', 'generateExperimentalApisTable', 'generateDeprecatedApisTable'

	// enable the Asciidoctor Diagram extension
	requires 'asciidoctor-diagram'

	separateOutputDirs false
	sources {
		include '**/index.adoc'
	}
	resources {
		from(sourceDir) {
			include '**/images/**'
		}
	}

	backends 'html5', 'pdf'

	attributes	'jupiter-version': version,
				'platform-version': platformVersion,
				'vintage-version': vintageVersion,
				'bom-version': version,
				'junit4-version': junit4Version,
				'apiguardian-version': apiGuardianVersion,
				'ota4j-version': ota4jVersion,
				'surefire-version': surefireVersion,
				'release-branch': releaseBranch,
				'docs-version': docsVersion,
				'revnumber' : version,
				'releaseBranch' : version,
				'testDir': sourceSets.test.java.srcDirs[0],
				'kotlinTestDir': sourceSets.test.kotlin.srcDirs[0],
				'consoleLauncherOptionsFile': consoleLauncherOptionsFile.toString(),
				'experimentalApisTableFile': experimentalApisTableFile.toString(),
				'deprecatedApisTableFile': deprecatedApisTableFile.toString(),
				'testResourcesDir': sourceSets.test.resources.srcDirs[0],
				'outdir': outputDir.absolutePath,
				'source-highlighter': 'coderay@', // TODO switch to 'rouge' once supported by the html5 backend and on MS Windows
				'tabsize': '4',
				'toc': 'left',
				'icons': 'font',
				'sectanchors': true,
				'idprefix': '',
				'idseparator': '-'
}


eclipse {
	classpath {
		plusConfigurations += [ project(':junit-platform-console').configurations.shadowed ]
		plusConfigurations += [ project(':junit-jupiter-params').configurations.shadowed ]

	}
}

idea {
	module {
		scopes.PROVIDED.plus += [ project(':junit-platform-console').configurations.shadowed ]
		scopes.PROVIDED.plus += [ project(':junit-jupiter-params').configurations.shadowed ]
	}
}

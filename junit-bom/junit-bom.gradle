description = rootProject.description + " (Bill of Materials)"

configurations.archives.artifacts.clear()
artifacts {
	archives file("README.md") // work around GRADLE-2406 by attaching text artifact
}

apply from: "$rootDir/gradle/maven.gradle"

uploadArchives.enabled = true

def mavenInstaller = install.repositories.mavenInstaller
def mavenDeployer = uploadArchives.repositories.mavenDeployer

[mavenInstaller, mavenDeployer]*.pom {
	project {
		artifactId = project.name
		description = project.description
	}
	whenConfigured {
		packaging = 'pom'
		dependencies = []
		withXml {
			asNode().children().last() + {
				delegate.dependencyManagement {
					delegate.dependencies {
						parent.mavenizedProjects.sort()
							.findAll { name -> name != 'junit-platform-console-standalone' }
							.collect { name -> rootProject.project(name) }
							.each { project ->
								delegate.dependency {
									delegate.groupId(project.group)
									delegate.artifactId(project.name)
									delegate.version(project.version)
								}
							}
					}
				}
			}
		}
	}
}

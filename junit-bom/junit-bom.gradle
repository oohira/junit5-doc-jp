description = rootProject.description + ' (Bill of Materials)'

apply from: "$rootDir/gradle/publishing.gradle"

publishing.publications.maven.pom {
	packaging = 'pom'
	description = 'This Bill of Materials POM can be used to ease dependency management ' +
			'when referencing multiple JUnit artifacts using Gradle or Maven.'
	withXml {
		def root = asNode()
		root.get('dependencies').each { root.remove(it) }
		root.children().last() + {
			delegate.dependencyManagement {
				delegate.dependencies {
					parent.mavenizedProjects.sort()
						.findAll { name -> name != 'junit-platform-console-standalone' }
						.collect { name -> rootProject.project(name) }
						.each { project ->
							delegate.dependency {
								delegate.groupId(project.group)
								delegate.artifactId(project.name)
								delegate.version(project.version)
							}
						}
				}
			}
		}
	}
}

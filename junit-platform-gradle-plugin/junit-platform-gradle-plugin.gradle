plugins {
	id 'pl.droidsonroids.jacoco.testkit' version '1.0.3'
	id 'java-gradle-plugin'
}

apply from: 'gradle/functional-test.gradle'

generateJacocoTestKitProperties {
	destinationFile = file("$buildDir/jacoco/testkit.exec")
	onlyIf { project.hasProperty('enableJaCoCo') }
}
functionalTest.dependsOn(generateJacocoTestKitProperties)

dependencies {
	api(project(path: ':junit-platform-console', configuration: 'shadow'))
	api(project(':junit-platform-launcher'))
	testRuntimeOnly("junit:junit:${junit4Version}")
	testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
		exclude module: 'groovy-all'
	}
}

processResources {
	inputs.properties( version: version.toString() )
	from(sourceSets.main.resources.srcDirs) {
		include '**/version.properties'
		filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [VERSION: version.toString()])
	}
}

compileGroovy.doLast {
	// Enable JAR manifest generation
	parent.generateManifest = true
}

jar {
	manifest {
		attributes(
			'Automatic-Module-Name': 'org.junit.platform.gradle.plugin'
		)
	}
}

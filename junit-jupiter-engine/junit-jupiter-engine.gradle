apply plugin: 'org.junit.platform.gradle.plugin'

junitPlatform {
	filters {
		engines {
			include 'junit-jupiter'
		}
		includeClassNamePattern '.*Tests?'
	}
	logManager 'org.apache.logging.log4j.jul.LogManager'
}

/*
test {
	scanForTestClasses = false
	include(['org/junit/jupiter/JupiterTestSuite.class'])
}
*/

// Disable these tests on Java 10 as they depend on working
// Kotlin compilation.
if (!gradle.ext.kotlinIsSupported) {
	sourceSets {
		test {
			java {
				srcDir 'src'
				exclude '**/AtypicalJvmMethodNameTests**'
				exclude '**/TestInstanceLifecycleKotlinTests**'
			}
		}
	}
}

jar {
	manifest {
		attributes(
			'Automatic-Module-Name': 'org.junit.jupiter.engine'
		)
	}
}

configurations {
	testArtifacts.extendsFrom testRuntime
}

task testJar(type: Jar) {
	classifier 'test'
	from sourceSets.test.output
}

artifacts {
	testArtifacts testJar
}

dependencies {
	api(project(':junit-platform-engine'))
	api(project(':junit-jupiter-api'))

	testImplementation(project(':junit-platform-launcher'))
	testImplementation(project(':junit-platform-runner'))
	testImplementation(project(path: ':junit-platform-engine', configuration: 'testArtifacts'))
	testImplementation("org.assertj:assertj-core:${assertJVersion}")
	testImplementation("org.mockito:mockito-core:${mockitoVersion}")
	if (gradle.ext.kotlinIsSupported) testImplementation("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")

	// Include junit-platform-console so that the JUnit Gradle plugin
	// uses the local version of the ConsoleLauncher.
	testRuntimeOnly(project(path: ':junit-platform-console', configuration: 'shadow'))

	testRuntimeOnly("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	testRuntimeOnly("org.apache.logging.log4j:log4j-jul:${log4jVersion}")
}

// Require Java 9 or higher
int javaVersion = Integer.valueOf(JavaVersion.current().getMajorVersion())
if (javaVersion < 9) {
	throw new GradleException("The JUnit 5 build requires Java 9 or higher. " +
		"Currently executing with Java " + javaVersion + ".")
}

// Disable Kotlin support on Java 10 or higher
// Details at https://github.com/junit-team/junit5/issues/1169
// Blocked by https://youtrack.jetbrains.com/issue/KT-21303
gradle.ext.kotlinIsSupported = javaVersion < 10

rootProject.name = 'junit5'

include 'documentation'
include 'junit-jupiter-api'
include 'junit-jupiter-engine'
include 'junit-jupiter-migrationsupport'
include 'junit-jupiter-params'
include 'junit-platform-commons'
include 'junit-platform-commons-java-9'
include 'junit-platform-console'
include 'junit-platform-console-standalone'
include 'junit-platform-engine'
include 'junit-platform-gradle-plugin'
include 'junit-platform-launcher'
include 'junit-platform-runner'
include 'junit-platform-suite-api'
include 'junit-platform-surefire-provider'
include 'junit-vintage-engine'
include 'platform-tests'

// check that every subproject has a custom build file
// based on the project name
rootProject.children.each { project ->
	project.buildFileName = "${project.name}.gradle"
	assert project.buildFile.isFile()
}

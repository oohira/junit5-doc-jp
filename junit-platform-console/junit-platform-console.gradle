apply plugin: 'com.github.johnrengelman.shadow'

description = 'JUnit Platform Console'

dependencies {
	api(project(':junit-platform-launcher'))

	shadowed('info.picocli:picocli:3.5.1')
}

jar {
	manifest {
		attributes(
			'Main-Class': 'org.junit.platform.console.ConsoleLauncher',
			'Automatic-Module-Name': 'org.junit.platform.console'
		)
	}
}

jar.enabled = false
jar.dependsOn shadowJar

shadowJar {
	// Generate shadow jar only if the underlying manifest was regenerated.
	// See https://github.com/junit-team/junit5/issues/631
	onlyIf {
		(project.generateManifest || !shadowJar.archivePath.exists())
	}
	classifier = null
	configurations = [project.configurations.shadowed]
	relocate 'picocli', 'org.junit.platform.console.shadow.picocli'
	from(projectDir) {
		include 'LICENSE-picocli.md'
		into 'META-INF'
	}
}
